dependencies {
    implementation(project(":newrelic-security-api"))
    implementation("com.newrelic.agent.java:newrelic-api:${nrAPIVersion}")
    implementation("com.newrelic.agent.java:newrelic-weaver-api:${nrAPIVersion}")
    implementation platform('software.amazon.awssdk:bom:2.1.2')
    implementation("software.amazon.awssdk:dynamodb:2.1.2")

    testImplementation("com.amazonaws:DynamoDBLocal:1.16.0")
    testImplementation("com.almworks.sqlite4java:sqlite4java:1.0.392")
    testImplementation("com.almworks.sqlite4java:libsqlite4java-osx:1.0.392")
    testImplementation("com.almworks.sqlite4java:libsqlite4java-linux-i386:1.0.392")
    testImplementation("com.almworks.sqlite4java:libsqlite4java-linux-amd64:1.0.392")
}

repositories {
    mavenCentral()
    maven {
        url 'https://s3-us-west-2.amazonaws.com/dynamodb-local/release'
    }
}

jar {
    manifest { attributes 'Implementation-Title': 'com.newrelic.instrumentation.security.dynamodb-2.1.2' }
}

verifyInstrumentation {
    passes 'software.amazon.awssdk:dynamodb:[2.1.2,2.15.34)'
    exclude 'software.amazon.awssdk:dynamodb:2.17.200' // this version failed the test, but the next one works again.
}

task copyNativeDeps(type: Copy) {
    from(configurations.testCompileClasspath) {
        include "*.so"
        include "*.dylib"
    }
    into "build/nr-native-libs"
}

test {
    dependsOn copyNativeDeps
    jvmArgs(["-Dsqlite4java.library.path=build/nr-native-libs"])
}

site {
    title 'AWS DynamoDB'
    type 'Datastore'
}
